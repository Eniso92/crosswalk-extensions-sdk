# Copyright (c) 2015 Intel Corporation. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Generate .js files from .cc files.
#
# Variables
#   sources: Paths to .js files to compile.
#
# Example
#   xwalk_js2c("foo_js") {
#     sources = [
#       "foo_api.js",
#     ]
#   }
# Output
#   foo_api.cc will be generated into ${gen_dir}.
template("xwalk_js2c") {
  #set_sources_assignment_filter([ "*.js" ])

  assert(defined(invoker.sources),
         "Need sources in $target_name listing the .js files.")

  gen_target_name = target_name + "_gen"
  action_foreach(gen_target_name) {
    script = "//tools/generate_api.py"
    sources = invoker.sources
    outputs = [ "$target_gen_dir/{{source_name_part}}.cc" ]

    args = [
      "{{source}}",
      "kSource_{{source_name_part}}",
      "{{output}}",
    ]
  }

  source_set(target_name) {
    sources = get_target_outputs(":$gen_target_name")
    deps = [ ":$gen_target_name" ]
  }
}

# Generate C++ (.cc and .h) files from .idl file.
#
# Variables
#   sources: Paths to .idl file to compile.
#
# Example
#   xwalk_idlgen("baz_idl") {
#     namespace = "foo::bar"
#     sources = [
#       "baz.idl",
#     ]
#   }
#
# Output
#   baz.[cc|h] will be generated into ${gen_dir} with namespace foo::bar::baz
template("xwalk_idlgen") {
  assert(defined(invoker.namespace),
         "Need namespace in $target_name.")
  assert(defined(invoker.sources),
         "Need sources in $target_name the .idl files.")

  gen_target_name = target_name + "_gen"
  action_foreach(gen_target_name) {
    script = "//tools/json_schema_compiler/compiler.py"
    sources = invoker.sources
    outputs = [
      "$target_gen_dir/{{source_name_part}}.cc",
      "$target_gen_dir/{{source_name_part}}.h",
    ]

    # FIXME (halton):
    # If we pass "$target_gen_dir" as destdir directly, files will be generated into wrong location.
    # For eg: extensions/foo/bar/bar.idl will be generated into
    # out/Debug/gen/extensions/extensions/extensions/foo/bar/, there is extra extensions in the path.
    # While xwalk/common/common.idl will be correctly generated into out/Debug/gen/xwalk/common/
    #
    # We use use no_hack as workaround to control whether to remove the extra path. To reduce the 
    # impact for extensions folder, this variable is only needed defined in xwalk/common/BUILD.gn.
    if (defined(invoker.no_hack) && invoker.no_hack) {
      destdir = rebase_path("$target_gen_dir")
    } else {
      destdir = get_path_info(rebase_path("$target_gen_dir"), "dir")
    }

    namespace = invoker.namespace + "::%(namespace)s"

    args = [
      "{{source}}",
      "--root=.",
      "--destdir=$destdir",
      "--namespace=$namespace",
      "--generator=cpp",
    ]
  }

  source_set(target_name) {
    sources = get_target_outputs(":$gen_target_name")
    deps = [ ":$gen_target_name" ]
  }
}
